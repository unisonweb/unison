<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd"><html xmlns="http://www.w3.org/1999/xhtml"><head><meta http-equiv="Content-Type" content="text/html; charset=UTF-8" /><meta name="viewport" content="width=device-width, initial-scale=1" /><title>Crypto.Tutorial</title><link href="linuwial.css" rel="stylesheet" type="text/css" title="Linuwial" /><link rel="stylesheet" type="text/css" href="quick-jump.css" /><link rel="stylesheet" type="text/css" href="https://fonts.googleapis.com/css?family=PT+Sans:400,400i,700" /><script src="haddock-bundle.min.js" async="async" type="text/javascript"></script><script type="text/x-mathjax-config">MathJax.Hub.Config({ tex2jax: { processClass: "mathjax", ignoreClass: ".*" } });</script><script src="https://cdnjs.cloudflare.com/ajax/libs/mathjax/2.7.5/MathJax.js?config=TeX-AMS-MML_HTMLorMML" type="text/javascript"></script></head><body><div id="package-header"><span class="caption">crypton-0.34: Cryptography Primitives sink</span><ul class="links" id="page-menu"><li><a href="src/Crypto.Tutorial.html">Source</a></li><li><a href="index.html">Contents</a></li><li><a href="doc-index.html">Index</a></li></ul></div><div id="content"><div id="module-header"><table class="info"><tr><th>Safe Haskell</th><td>Safe-Inferred</td></tr><tr><th>Language</th><td>Haskell2010</td></tr></table><p class="caption">Crypto.Tutorial</p></div><div id="table-of-contents"><div id="contents-list"><p class="caption" onclick="window.scrollTo(0,0)">Contents</p><ul><li><a href="#g:1">API design</a></li><li><a href="#g:2">Hash algorithms</a></li><li><a href="#g:3">Symmetric block ciphers</a></li><li><a href="#g:4">Combining primitives</a></li></ul></div></div><div id="description"><p class="caption">Description</p><div class="doc"><p>Examples of how to use <code>crypton</code>.</p></div></div><div id="synopsis"><details id="syn"><summary>Synopsis</summary><ul class="details-toggle" data-details-id="syn"></ul></details></div><div id="interface"><a href="#g:1" id="g:1"><h1>API design</h1></a><div class="doc"><p>APIs in crypton are often based on type classes from package
 <a href="https://hackage.haskell.org/package/memory">memory</a>, notably
 <code><a href="../memory-0.18.0/Data-ByteArray.html#v:ByteArrayAccess" title="Data.ByteArray">ByteArrayAccess</a></code> and <code><a href="../memory-0.18.0/Data-ByteArray.html#v:ByteArray" title="Data.ByteArray">ByteArray</a></code>.
 Module <a href="../memory-0.18.0/Data-ByteArray.html">Data.ByteArray</a> provides many primitives that are useful to
 work with crypton types.  For example function <code><a href="../memory-0.18.0/Data-ByteArray.html#v:convert" title="Data.ByteArray">convert</a></code>
 can transform one <code><a href="../memory-0.18.0/Data-ByteArray.html#v:ByteArrayAccess" title="Data.ByteArray">ByteArrayAccess</a></code> concrete type like
 <code><a href="Crypto-Hash.html#v:Digest" title="Crypto.Hash">Digest</a></code> to a <code><a href="../bytestring-0.11.5.3/Data-ByteString.html#v:ByteString" title="Data.ByteString">ByteString</a></code>.</p><p>Algorithms and functions needing random bytes are based on type class
 <code><a href="Crypto-Random-Types.html#v:MonadRandom" title="Crypto.Random.Types">MonadRandom</a></code>.  Implementation <code><a href="../base-4.18.2.1/System-IO.html#t:IO" title="System.IO">IO</a></code> uses a system source
 of entropy.  It is also possible to use a <code><a href="Crypto-Random-Types.html#v:DRG" title="Crypto.Random.Types">DRG</a></code> with
 <code><a href="Crypto-Random-Types.html#v:MonadPseudoRandom" title="Crypto.Random.Types">MonadPseudoRandom</a></code></p><p>Error conditions are returned with data type <code><a href="Crypto-Error.html#v:CryptoFailable" title="Crypto.Error">CryptoFailable</a></code>.
 Functions in module <a href="Crypto-Error.html">Crypto.Error</a> can convert those values to runtime
 exceptions, <code><a href="../base-4.18.2.1/Data-Maybe.html#t:Maybe" title="Data.Maybe">Maybe</a></code> or <code><a href="../base-4.18.2.1/Data-Either.html#t:Either" title="Data.Either">Either</a></code> values.</p></div><a href="#g:2" id="g:2"><h1>Hash algorithms</h1></a><div class="doc"><p>Hashing a complete message:</p><pre>import Crypto.Hash

import Data.ByteString (ByteString)

exampleHashWith :: ByteString -&gt; IO ()
exampleHashWith msg = do
    putStrLn $ &quot;  sha1(&quot; ++ show msg ++ &quot;) = &quot; ++ show (hashWith SHA1   msg)
    putStrLn $ &quot;sha256(&quot; ++ show msg ++ &quot;) = &quot; ++ show (hashWith SHA256 msg)</pre><p>Hashing incrementally, with intermediate context allocations:</p><pre>{-# LANGUAGE OverloadedStrings #-}

import Crypto.Hash

import Data.ByteString (ByteString)

exampleIncrWithAllocs :: IO ()
exampleIncrWithAllocs = do
    let ctx0 = hashInitWith SHA3_512
        ctx1 = hashUpdate ctx0 (&quot;The &quot;   :: ByteString)
        ctx2 = hashUpdate ctx1 (&quot;quick &quot; :: ByteString)
        ctx3 = hashUpdate ctx2 (&quot;brown &quot; :: ByteString)
        ctx4 = hashUpdate ctx3 (&quot;fox &quot;   :: ByteString)
        ctx5 = hashUpdate ctx4 (&quot;jumps &quot; :: ByteString)
        ctx6 = hashUpdate ctx5 (&quot;over &quot;  :: ByteString)
        ctx7 = hashUpdate ctx6 (&quot;the &quot;   :: ByteString)
        ctx8 = hashUpdate ctx7 (&quot;lazy &quot;  :: ByteString)
        ctx9 = hashUpdate ctx8 (&quot;dog&quot;    :: ByteString)
    print (hashFinalize ctx9)</pre><p>Hashing incrementally, updating context in place:</p><pre>{-# LANGUAGE OverloadedStrings #-}

import Crypto.Hash.Algorithms
import Crypto.Hash.IO

import Data.ByteString (ByteString)

exampleIncrInPlace :: IO ()
exampleIncrInPlace = do
    ctx &lt;- hashMutableInitWith SHA3_512
    hashMutableUpdate ctx (&quot;The &quot;   :: ByteString)
    hashMutableUpdate ctx (&quot;quick &quot; :: ByteString)
    hashMutableUpdate ctx (&quot;brown &quot; :: ByteString)
    hashMutableUpdate ctx (&quot;fox &quot;   :: ByteString)
    hashMutableUpdate ctx (&quot;jumps &quot; :: ByteString)
    hashMutableUpdate ctx (&quot;over &quot;  :: ByteString)
    hashMutableUpdate ctx (&quot;the &quot;   :: ByteString)
    hashMutableUpdate ctx (&quot;lazy &quot;  :: ByteString)
    hashMutableUpdate ctx (&quot;dog&quot;    :: ByteString)
    hashMutableFinalize ctx &gt;&gt;= print</pre></div><a href="#g:3" id="g:3"><h1>Symmetric block ciphers</h1></a><div class="doc"><pre>{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE ScopedTypeVariables #-}
{-# LANGUAGE GADTs #-}

import           Crypto.Cipher.AES (AES256)
import           Crypto.Cipher.Types (BlockCipher(..), Cipher(..), nullIV, KeySizeSpecifier(..), IV, makeIV)
import           Crypto.Error (CryptoFailable(..), CryptoError(..))

import qualified Crypto.Random.Types as CRT

import           Data.ByteArray (ByteArray)
import           Data.ByteString (ByteString)

-- | Not required, but most general implementation
data Key c a where
  Key :: (BlockCipher c, ByteArray a) =&gt; a -&gt; Key c a

-- | Generates a string of bytes (key) of a specific length for a given block cipher
genSecretKey :: forall m c a. (CRT.MonadRandom m, BlockCipher c, ByteArray a) =&gt; c -&gt; Int -&gt; m (Key c a)
genSecretKey _ = fmap Key . CRT.getRandomBytes

-- | Generate a random initialization vector for a given block cipher
genRandomIV :: forall m c. (CRT.MonadRandom m, BlockCipher c) =&gt; c -&gt; m (Maybe (IV c))
genRandomIV _ = do
  bytes :: ByteString &lt;- CRT.getRandomBytes $ blockSize (undefined :: c)
  return $ makeIV bytes

-- | Initialize a block cipher
initCipher :: (BlockCipher c, ByteArray a) =&gt; Key c a -&gt; Either CryptoError c
initCipher (Key k) = case cipherInit k of
  CryptoFailed e -&gt; Left e
  CryptoPassed a -&gt; Right a

encrypt :: (BlockCipher c, ByteArray a) =&gt; Key c a -&gt; IV c -&gt; a -&gt; Either CryptoError a
encrypt secretKey initIV msg =
  case initCipher secretKey of
    Left e -&gt; Left e
    Right c -&gt; Right $ ctrCombine c initIV msg

decrypt :: (BlockCipher c, ByteArray a) =&gt; Key c a -&gt; IV c -&gt; a -&gt; Either CryptoError a
decrypt = encrypt

exampleAES256 :: ByteString -&gt; IO ()
exampleAES256 msg = do
  -- secret key needs 256 bits (32 * 8)
  secretKey &lt;- genSecretKey (undefined :: AES256) 32
  mInitIV &lt;- genRandomIV (undefined :: AES256)
  case mInitIV of
    Nothing -&gt; error &quot;Failed to generate and initialization vector.&quot;
    Just initIV -&gt; do
      let encryptedMsg = encrypt secretKey initIV msg
          decryptedMsg = decrypt secretKey initIV =&lt;&lt; encryptedMsg
      case (,) &lt;$&gt; encryptedMsg &lt;*&gt; decryptedMsg of
        Left err -&gt; error $ show err
        Right (eMsg, dMsg) -&gt; do
          putStrLn $ &quot;Original Message: &quot; ++ show msg
          putStrLn $ &quot;Message after encryption: &quot; ++ show eMsg
          putStrLn $ &quot;Message after decryption: &quot; ++ show dMsg</pre></div><a href="#g:4" id="g:4"><h1>Combining primitives</h1></a><div class="doc"><p>This example shows how to use Curve25519, XSalsa and Poly1305 primitives to
 emulate NaCl's <code>crypto_box</code> construct.</p><pre>import qualified Data.ByteArray as BA
import           Data.ByteString (ByteString)
import qualified Data.ByteString as B

import qualified Crypto.Cipher.XSalsa as XSalsa
import qualified Crypto.MAC.Poly1305 as Poly1305
import qualified Crypto.PubKey.Curve25519 as X25519

-- | Build a @crypto_box@ packet encrypting the specified content with a
-- 192-bit nonce, receiver public key and sender private key.
crypto_box content nonce pk sk = BA.convert tag `B.append` c
  where
    zero         = B.replicate 16 0
    shared       = X25519.dh pk sk
    (iv0, iv1)   = B.splitAt 8 nonce
    state0       = XSalsa.initialize 20 shared (zero `B.append` iv0)
    state1       = XSalsa.derive state0 iv1
    (rs, state2) = XSalsa.generate state1 32
    (c, _)       = XSalsa.combine state2 content
    tag          = Poly1305.auth (rs :: ByteString) c

-- | Try to open a @crypto_box@ packet and recover the content using the
-- 192-bit nonce, sender public key and receiver private key.
crypto_box_open packet nonce pk sk
    | B.length packet &lt; 16 = Nothing
    | BA.constEq tag' tag  = Just content
    | otherwise            = Nothing
  where
    (tag', c)    = B.splitAt 16 packet
    zero         = B.replicate 16 0
    shared       = X25519.dh pk sk
    (iv0, iv1)   = B.splitAt 8 nonce
    state0       = XSalsa.initialize 20 shared (zero `B.append` iv0)
    state1       = XSalsa.derive state0 iv1
    (rs, state2) = XSalsa.generate state1 32
    (content, _) = XSalsa.combine state2 c
    tag          = Poly1305.auth (rs :: ByteString) c</pre></div></div></div><div id="footer"><p>Produced by <a href="http://www.haskell.org/haddock/">Haddock</a> version 2.29.2</p></div></body></html>