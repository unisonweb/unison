This file tests some obscure issues involved with abilities and over-applied
functions.

```unison
Stream.fromList : [a] -> '{Stream a} ()
Stream.fromList l _ =
  _ = List.map (x -> emit x) l
  ()

Stream.map : (a -> b) -> '{Stream a} r -> '{Stream b} r
Stream.map f stream = handle !stream with Stream.map.handler f

Stream.map.handler : (a -> b) -> Request {Stream a} r -> '{Stream b} r
Stream.map.handler f = cases 
  {Stream.emit a -> resume} -> 'let
    Stream.emit (f a)
    Stream.map f resume ()
  {r} -> 'r

increment : Nat -> Nat
increment n = 1 + n

> Stream.toList (Stream.map increment (Stream.fromList [1,2,3]))

> let
    s1 = do emit 10 
            emit 20
            emit 30
            emit 40
    s2 = Stream.map (a -> a * 10) s1
    Stream.toList s2 
```

```ucm

  I found and typechecked these definitions in scratch.u. If you
  do an `add` or `update`, here's how your codebase would
  change:
  
    ⍟ These new definitions are ok to `add`:
    
      Stream.fromList    : [a] -> '{Stream a} ()
      Stream.map         : (a -> b)
                           -> '{Stream a} r
                           -> '{Stream b} r
      Stream.map.handler : (a -> b)
                           -> Request {Stream a} r
                           -> '{Stream b} r
      increment          : Nat -> Nat
  
  Now evaluating any watch expressions (lines starting with
  `>`)... Ctrl+C cancels.

    19 | > Stream.toList (Stream.map increment (Stream.fromList [1,2,3]))
           ⧩
           [2, 3, 4]
  
    21 | > let
           ⧩
           [100, 200, 300, 400]

```
```unison
structural ability E where
  eff : () -> ()

hh : Request {E} (Nat ->{} r) -> Nat -> r
hh = cases
  {eff _ -> k} -> x -> h k x
  {x} -> x

h : '{E} (Nat -> r) -> Nat -> r
h k = handle !k with hh

foo : '{E} (Nat -> Nat)
foo _ =
  eff ()
  x -> 7

> h foo 337
```

```ucm

  I found and typechecked these definitions in scratch.u. If you
  do an `add` or `update`, here's how your codebase would
  change:
  
    ⍟ These new definitions are ok to `add`:
    
      structural ability E
      foo : '{E} (Nat -> Nat)
      h   : '{E} (Nat -> r) -> Nat -> r
      hh  : Request {E} (Nat -> r) -> Nat -> r
  
  Now evaluating any watch expressions (lines starting with
  `>`)... Ctrl+C cancels.

    17 | > h foo 337
           ⧩
           7

```
```unison
structural ability Over where
  over : Nat ->{Over} (Nat -> Nat)

hd = cases
  {over m -> k} ->
    handle k (n -> m + n) with hd
  {x} -> x

delegated _ =
  handle over 5 with hd

hmm =
  x : Nat
  x = delegated () 16789
  trace "x" x
  x

> hmm
```

```ucm

  I found and typechecked these definitions in scratch.u. If you
  do an `add` or `update`, here's how your codebase would
  change:
  
    ⍟ These new definitions are ok to `add`:
    
      structural ability Over
      delegated : ∀ _. _ -> Nat -> Nat
      hd        : Request {g, Over} x -> x
      hmm       : Nat
  
  Now evaluating any watch expressions (lines starting with
  `>`)... Ctrl+C cancels.

    18 | > hmm
           ⧩
           16794

```
