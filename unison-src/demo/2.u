
merge : (a -> a -> Boolean) -> [a] -> [a] -> [a]
merge lte a b =
  use Sequence ++
  use Optional None Some
  go acc a b = case at 0 a of
    None -> acc ++ b
    Some hd1 -> case at 0 b of
      None -> acc ++ a
      Some hd2 ->
        if hd1 `lte` hd2 then go (acc `snoc` hd1) (drop 1 a) b
        else go (acc `snoc` hd2) a (drop 1 b)
  go [] a b

-- let's make sure it works
-- > merge (<) [1,3,4,99,504,799] [0,19,22,23]

sort : (a -> a -> Boolean) -> [a] -> [a]
sort lte a =
  if Sequence.size a < 2 then a
  else
    l = sort lte (take (size a / 2) a)
    r = sort lte (drop (size a / 2) a)
    merge lte l r

-- let's make sure it works

-- > sort (<) [3,2,1,1,2,3,9182,1,2,34,1,23]

-- > sort (<) ["Dave", "Carol", "Eve", "Alice", "Bob", "Francis", "Hal", "Illy", "Joanna", "Greg", "Karen"]

-- > sort (<) [3,2,1,1,2,3,9182,1,2,34,1,"oops"]

-- > merge (<) [1,4,5,90,102] ["a", "b"]
